<程序>→<分程序>.
<分程序>→ [<常量说明部分>][<变量说明部分>][<过程说明部分>]<语句>
<常量说明部分> → CONST<常量定义>{ ,<常量定义>};
<常量定义> → <标识符>=<无符号整数>
<无符号整数> → <数字>{<数字>}
<变量说明部分> → VAR<标识符>{ ,<标识符>};
<标识符> → <字母>{<字母>|<数字>}
<过程说明部分> → <过程首部><分程序>;{<过程说明部分>}
<过程首部> → PROCEDURE <标识符>;
<语句> → <赋值语句>|<条件语句>|<当型循环语句>|<过程调用语句>|<读语句>|<写语句>|<复合语句>|<空语句>
<赋值语句> → <标识符>:=<表达式>
<复合语句> → BEGIN<语句>{ ;<语句>} END
<条件> → <表达式><关系运算符><表达式>|ODD<表达式>
<表达式> → [+|-]<项>{<加减运算符><项>}
<项> → <因子>{<乘除运算符><因子>}
<因子> → <标识符>|<无符号整数>|(<表达式>)
<加减运算符> → +|-
<乘除运算符> → *|/
<关系运算符> → =|#|<|<=|>|>=
<条件语句> → IF<条件>THEN<语句>
<过程调用语句> → CALL<标识符>
<当型循环语句> → WHILE<条件>DO<语句>
<读语句> → READ(<标识符>{ ,<标识符>})
<写语句> → WRITE(<标识符>{,<标识符>})
<字母> → A|B|C…X|Y|Z
<数字> → 0|1|2…7|8|9
<空语句> → epsilon

上下文无关文法中的节点	对应的替换词语
程序	PROGRAM
分程序	SUBPROG
常量说明部分	CONSTANTDECLARE
常量定义	CONSTANTDEFINE
无符号整数	< 这是一个叶子节点，用其本身替代 >
变量说明部分	VARIABLEDECLARE
标识符	< 这是一个叶子节点，用其本身替代 >
过程说明部分	PROCEDUREDECLARE
过程首部	PROCEDUREHEAD
语句	SENTENCE
赋值语句	ASSIGNMENT
复合语句	COMBINED
条件	CONDITION
表达式	EXPRESSION
项	ITEM
因子	FACTOR
加减运算符	< 这是一个叶子节点，用其本身替代 >
乘除运算符	< 这是一个叶子节点，用其本身替代 >
关系运算符	< 这是一个叶子节点，用其本身替代 >
条件语句	IFSENTENCE
过程调用语句	CALLSENTENCE
当型循环语句	WHILESENTENCE
读语句	READSENTENCE
写语句	WRITESENTENCE
空语句	EMPTY